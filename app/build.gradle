apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

android {
  compileSdkVersion 29
  buildToolsVersion "29.0.2"
  defaultConfig {
    applicationId "risq.android"
      minSdkVersion 26
      targetSdkVersion 29
      versionCode 4
      versionName "0.1.2-dev"
      testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
  }
  if(project.hasProperty("storeFile")) {
    signingConfigs {
      release {
        storeFile = file(project.getProperty("storeFile"))
        storePassword = project.getProperty("storePassword")
        keyAlias = project.getProperty("keyAlias")
        keyPassword = project.getProperty("keyPassword")
      }
    }
  }
  buildTypes {
    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
      if(project.hasProperty("storeFile")) {
        signingConfig signingConfigs.release
      }
    }
  }
}

apply plugin: 'com.apollographql.android'

apollo {
  generateKotlinModels = true
  customTypeMapping = [
    "MarketPair" : "kotlin.String"
  ]
}

apply plugin: 'org.mozilla.rust-android-gradle.rust-android'

cargo {
  module = "../rust/risq-glue"
  libname = "risq_glue"
  targets = ["arm", "x86", "arm64"]
  profile = project.hasProperty("cargoProfile") ? project.findProperty("cargoProfile") : "debug"
}

dependencies {
  implementation fileTree(dir: 'libs', include: ['*.jar'])
  implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
  implementation 'androidx.appcompat:appcompat:1.1.0'
  implementation 'androidx.core:core-ktx:1.1.0'
  implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
  testImplementation 'junit:junit:4.12'
  androidTestImplementation 'androidx.test.ext:junit:1.1.1'
  androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

  implementation "androidx.lifecycle:lifecycle-extensions:2.1.0"
  implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.1.0"

  implementation 'com.google.android.material:material:1.0.0'

  implementation "com.squareup.okhttp3:okhttp:4.2.1"

  implementation "com.apollographql.apollo:apollo-android-support:$versions.apolloVersion"
  implementation "com.apollographql.apollo:apollo-runtime:$versions.apolloVersion"
  implementation "com.apollographql.apollo:apollo-coroutines-support:$versions.apolloVersion"

  implementation 'org.torproject:tor-android-binary:0.4.1.5'
  implementation 'com.jrummyapps:android-shell:1.0.1'
}
